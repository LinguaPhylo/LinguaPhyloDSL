grammar lphy.dsl.LinguaPhylo with org.eclipse.xtext.xbase.Xbase

generate linguaPhylo "http://www.dsl.lphy/LinguaPhylo"

import "http://www.eclipse.org/Xtext/Xbase/XAnnotations" as xAnnotations

LPhyModel:
	{LPhyModel}
	('package' package=QualifiedName)?
	importSection=XImportSection?
	'model' name=ID '{'
    (lawNodes += LawNode)*
    '}'
;
	
LawNode:
  (=> InstantiatedDistribution)
;

// e.g.: y1 ~ Normal(mean=mean, variance=variance)
InstantiatedDistribution:
  // random variables generated
	generatedVariables = Variable 
	// type of the distribution
	typeSpec = DistributionTypeSpecification //distributionType = [BlangModel|QualifiedName] 
	// arguments in the constructor of the distribution
  (=> explicitArguments ?=
    '('
      ( (arguments += Argument) ( ',' arguments += Argument)* )?
    ')'
  )?	
;

Argument:
	name=ID '=' value=XExpression	
;

Variable:
	name=ID ( '[' range=Range ']')?		
;

Range:
	start=INT (':' end=INT)?
;

DistributionTypeSpecification:
  'is' JavaDist | 
  '~' LPhyDist // UTF-8: 7E
;

LPhyDist:
  distributionType = [LPhyModel|QualifiedName]
;

JavaDist:
  distributionType = JvmTypeReference
;

@Override
terminal ID: '^'?('a'..'z'|'A'..'Z'|'α'..'ω'|'Α'..'Ω')('a'..'z'|'A'..'Z'|'α'..'ω'|'Α'..'Ω'|'0'..'9')*
;
